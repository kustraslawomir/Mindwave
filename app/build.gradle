plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

ext {
    APP_NAME = "Focus"
    APP_NAME_DEBUG = "Focus.debug"
}

android {
    namespace 'skustra.focusflow'
    compileSdk 33

    defaultConfig {
        applicationId "skustra.focus"
        minSdk 26
        targetSdk 33
        versionCode 3
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {

        def PROPERTIES_PATH = 'C:\\Users\\s.kustra\\Documents\\Focus\\signing.properites'
        def JKS_PATH = 'C:\\Users\\s.kustra\\Documents\\Focus\\focus_key.jks'

        Properties properties = new Properties()
        properties.load(project.rootProject
                .file(PROPERTIES_PATH)
                .newDataInputStream())

        release {
            storeFile file(new File(JKS_PATH))
            storePassword properties.getProperty('RELEASE_KEYSTORE_PASSWORD')
            keyAlias properties.getProperty('RELEASE_KEYSTORE_ALIAS')
            keyPassword properties.getProperty('RELEASE_KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: APP_NAME]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: APP_NAME_DEBUG]
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def SEP = "_"
                def version = variant.versionName
                def date = new Date()
                def formattedDate = date.format('dd-MM-yy HH-mm')

                def newApkName = "v" + version + SEP + formattedDate + ".apk"

                output.outputFileName = newApkName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.compose.material:material:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "com.google.accompanist:accompanist-permissions:0.31.2-alpha"

    def compose_version = '1.5.0-beta01'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    def lifecycle_compose_version = '2.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_compose_version"
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.compose.material3:material3:1.2.0-alpha02'

    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    def hilt_version = "2.44.2"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    def room_version = "2.5.1"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def vico_version = "1.6.6"
    implementation "com.patrykandpatrick.vico:core:$vico_version"
    implementation "com.patrykandpatrick.vico:compose:$vico_version"
}

kapt {
    correctErrorTypes true
}

